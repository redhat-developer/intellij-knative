buildscript {
    ext.java_version = "17"
}

plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij' version '1.16.0'
}

group 'com.redhat.devtools.intellij'
version projectVersion

configurations {
    compileOptions {
        sourceCompatibility = java_version
        targetCompatibility = java_version
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://repository.jboss.org' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

def versionsMap = ['IU-2022.3':'223.7571.175', 'IU-2023.1':'231.8109.91', 'IU-2023.2':'232.8660.158', 'IU-2023.3':'233.11799.7', 'IU-2024.1':'241.14494.19']

intellij {
    version = ideaVersion
    pluginName = 'Knative by Red Hat'
    plugins = ['terminal',
               'yaml',
               'org.jetbrains.plugins.github',
               'com.intellij.kubernetes:' + versionsMap[ideaVersion],
               'com.redhat.devtools.intellij.telemetry:1.1.0.52']
    updateSinceUntilBuild = false
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

runIdeForUiTests {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

//with this option enabled, build will fail about IDEA expiration builds
buildSearchableOptions.enabled = false

dependencies {
    implementation 'io.fabric8:knative-client:7.0-SNAPSHOT'
    implementation 'io.fabric8:openshift-client:7.0-SNAPSHOT'
    implementation 'com.redhat.devtools.intellij:intellij-common:1.9.5'
    testImplementation 'org.mockito:mockito-inline:4.6.1'
    // telemetry contributes annotations 13.0.0, so we need to declare newer version
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'com.redhat.devtools.intellij:intellij-common:1.9.5:test'
}

configurations {
    runtimeClasspath {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    // Discover and execute JUnit4-based tests
    useJUnit()
    jvmArgs "-Djava.awt.headless=true"
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    jvmArgs "-Djava.awt.headless=true"
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }

runPluginVerifier {
    ideVersions = [ideaVersion]
}

publishPlugin {
    token    = jetBrainsToken
    channels = [jetBrainsChannel]
}
