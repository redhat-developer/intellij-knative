buildscript {
    /*
     * for more complex conversion ideaVersion -> sinceIdeaBuild
     * see https://github.com/rhdunn/xquery-intellij-plugin/blob/master/build.gradle#L1-L47
     */
    def since = ideaVersion =~ /I.-20(\d\d)\.([1-3])(\.\d+)?$/
    if (since.matches()) {
        ext.sinceIdeaBuild = "${since.group(1)}${since.group(2)}".toInteger()
    } else {
        ext.sinceIdeaBuild = 0
    }

    if (ext.sinceIdeaBuild >= 223) {
        ext.java_version = "17"
    } else {
        ext.java_version = "11"
    }
}

plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij' version '1.16.0'
}

group 'com.redhat.devtools.intellij'
version projectVersion

configurations {
    compileOptions {
        sourceCompatibility = java_version
        targetCompatibility = java_version
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://repository.jboss.org' }
}

def versionsMap = ['IU-2022.1':'221.5080.1', 'IU-2022.2':'222.3345.16', 'IU-2022.3':'223.7571.175', 'IU-2023.1':'231.8109.91', 'IU-2023.2':'232.8660.158']

intellij {
    version = ideaVersion
    pluginName = 'Knative by Red Hat'
    plugins = ['terminal',
               'yaml',
               'org.jetbrains.plugins.github',
               'com.intellij.kubernetes:' + versionsMap[ideaVersion],
               'com.redhat.devtools.intellij.telemetry:1.1.0.52',
               'com.redhat.devtools.intellij.kubernetes:1.2.0.271']
    updateSinceUntilBuild = false
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

runIdeForUiTests {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

//with this option enabled, build will fail about IDEA expiration builds
buildSearchableOptions.enabled = false

dependencies {
    implementation 'io.fabric8:knative-client:6.4.1'
    implementation 'com.redhat.devtools.intellij:intellij-common:1.9.1'
    implementation 'com.squareup.okio:okio:3.1.0'
    testImplementation 'org.mockito:mockito-inline:4.6.1'
    // telemetry contributes annotations 13.0.0, so we need to declare newer version
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'com.redhat.devtools.intellij:intellij-common:1.9.1:test'
}

configurations {
    runtimeClasspath {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    // Discover and execute JUnit4-based tests
    useJUnit()
    jvmArgs "-Djava.awt.headless=true"
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    jvmArgs "-Djava.awt.headless=true"
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }

runPluginVerifier {
    ideVersions = [ideaVersion]
}

publishPlugin {
    token    = jetBrainsToken
    channels = [jetBrainsChannel]
}
